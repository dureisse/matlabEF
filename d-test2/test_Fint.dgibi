* Test de calcul de forces interieures dues a un champ de contraintes

* Test intermediaire
TEST1 = 1;
* Test simple
TEST1 = 0;
* Test plus complexe
TEST1 = 2;

OPTI DIME 2 ELEM QUA4;
LOG1 = 1.;
P1 = 0. 0.;
P2 = 1. 0.;
P3 = 1. 1.;
P4 = 0. 1.;
M1 = MANU QUA4 P1 P2 P3 P4;
SI (TEST1 EGA 0);
  MAIL1 = M1;
SINON;
SI (TEST1 EGA 1);
  Q1 = P1 PLUS P4;
  Q2 = P2 PLUS P4;
  Q3 = P3 PLUS P4;
  Q4 = P4 PLUS P4;
  M2 = MANU QUA4 Q1 Q2 Q3 Q4;
  M2 = MANU QUA4 Q2 Q3 Q4 Q1;
  MAIL1 = M1 ET M2;
  ELIM MAIL1 0.0001;
SINON;
  Q1 = P1 PLUS P4;
  Q2 = P2 PLUS P4;
  Q3 = P3 PLUS P4;
  Q4 = P4 PLUS P4;
  M2 = MANU QUA4 Q1 Q2 Q3 Q4;
  M2 = MANU QUA4 Q2 Q3 Q4 Q1;
  M2 = M2 CHAN TRI3;
  MAIL1 = M1 ET M2;
  MAIL1 = MAIL1 ET (SYME MAIL1 DROITE P1 P4);
  MAIL1 = MAIL1 ET (SYME MAIL1 DROITE P1 P2);
  ELIM MAIL1 0.0001;
FINSI;
FINSI;

OPTI DIME 3 ELEM CUB8;
MAIL1 = MAIL1 VOLU TRAN 1 (0. 0. 1.);
X2 Y2 Z2 = COOR MAIL1;

SI (TEST1 EGA 2);
  MAIL1 = MAIL1 ET (SYME MAIL1 PLAN P1 P2 P4);
  ELIM MAIL1 0.0001;
* Deformer le maillage
  UU1 = (0.2 * (EXCO SCAL (X2 * Y2) UY NATURE DIFFUS)) ET
        (0.1 * (EXCO SCAL Z2 UX NATURE DIFFUS));
  MAIL1 = MAIL1 PLUS UU1;
  X2 Y2 Z2 = COOR MAIL1;
FINSI;

MODL1 = MODE MAIL1 MECANIQUE ELASTIQUE;
CH0 = MANU CHML MODL1 SCAL 0. 'GRAVITE';
X1 Y1 Z1 = COOR CH0;
SI ((TEST1 EGA 2) OU (TEST1 EGA 1));
* La contrainte pour matlab (Robin : Tau=racine(2)*Sigma)
  CH1 = (EXCO SCAL X1 SMXX) ET (EXCO SCAL Y1 SMYY) ET
    (EXCO SCAL Z1 SMZZ) ET (EXCO SCAL (X1*Y1) TAXY) ET
    (EXCO SCAL X1 TAXZ) ET (EXCO SCAL Y1 TAYZ);
* La contrainte pour Cast3M (Voigt, pour contrainte = idem)
  R2 = 2. ** -0.5;
  CCH1 = (EXCO SCAL X1 SMXX) ET (EXCO SCAL Y1 SMYY) ET
    (EXCO SCAL Z1 SMZZ) ET (R2 * (EXCO SCAL (X1*Y1) SMXY)) ET
    (R2 * (EXCO SCAL X1 SMXZ)) ET (R2 * (EXCO SCAL Y1 SMYZ));
SINON;
  CH1 = MANU CHML MODL1 SMXX 1. SMYY 0. SMZZ 0.
    TAXY 0. TAXZ 0. TAYZ 0. 'GRAVITE';
  CCH1 = MANU CHML MODL1 SMXX 1. SMYY 0. SMZZ 0.
    SMXY 0. SMXZ 0. SMYZ 0. 'GRAVITE';
FINSI;
Sig1 = CHAN CCH1 MODL1 'STRESSES';

SI ((TEST1 EGA 2) OU (TEST1 EGA 1));
  MAT1 = MATE MODL1 YOUNG 1. NU 0.3;
SINON;
  MAT1 = MATE MODL1 YOUNG 1. NU 0.;
FINSI;
CH2 = CH1 ET MAT1;

SI ((TEST1 EGA 2) OU (TEST1 EGA 1));
  DEP1 = (EXCO SCAL X2 UX NATURE DIFFUS);
  DEP2 = (EXCO SCAL Y2 UY NATURE DIFFUS);
  DEP3 = (EXCO SCAL Z2 UZ NATURE DIFFUS);
  DEP4 = (EXCO SCAL X2 UY NATURE DIFFUS);
  DEP5 = (EXCO SCAL Y2 UZ NATURE DIFFUS);
  DEP6 = (EXCO SCAL Z2 UX NATURE DIFFUS);
  DEPT1 = DEP1 ET DEP2 ET DEP3;
  DEPT1 = DEPT1 +  DEP4 + DEP5 + DEP6;
SINON;
  DEP1 = (EXCO SCAL X2 UX NATURE DIFFUS);
  DEP2 = 0. * (EXCO SCAL Y2 UY NATURE DIFFUS);
  DEP3 = 0. * (EXCO SCAL Z2 UZ NATURE DIFFUS);
  DEPT1 = DEP1 ET DEP2 ET DEP3;
FINSI;

* On calcule les forces interieures HPP
Fint1 = BSIGMA MODL1 Sig1;
* On calcule les forces interieures Green-Lagrange

* On calcule les deformations HPP
Eps1 = EPSI 'LINE' MODL1 DEPT1;
* On calcule les deformations Green-Lagrange
Eps2 = EPSI 'QUAD' MODL1 DEPT1;

OPTI SORT 'test_Fint';
SORT AVS MAIL1 DEPT1 CH2;
OPTI SORT 'poub';

FIN;
